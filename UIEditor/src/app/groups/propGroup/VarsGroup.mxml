<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:validators="app.validators.*"
		 fontFamily="微软雅黑"
		 width="100%" height="100%"
		 creationComplete="creationCompleteHandler(event)"
		 >
	<fx:Script>
		<![CDATA[
			import app.common.AppCommon;
			import app.common.ImageAssets;
			import app.effects.AppEffect;
			import app.layers.Layer;
			import app.utils.FormatUtil;
			
			import lolo.core.Common;
			
			import mx.events.FlexEvent;
			
			import spark.events.TextOperationEvent;
			
			
			/**FocusOut的时候，可能会先切换selectedLayer，所以这里需要自己保存着引用*/
			private var _layer:Layer;
			
			private var _isOpen:Boolean;
			
			
			
			/**
			 * 其他自定义变量
			 */
			protected function creationCompleteHandler(event:FlexEvent):void
			{
			}
			
			
			/**
			 * 更新显示内容
			 */
			public function update(glow:Boolean=false):void
			{
				_layer = AppCommon.controller.selectedLayer;
				_isOpen = selectedLayer.vars != "";
				
				var color:uint = 0x00FF00;
				try {
					varsText.text = selectedLayer.vars;
					varsText.text = FormatUtil.getJsonString(JSON.parse(selectedLayer.vars));
				}
				catch(error:Error) {
					color = 0xFF0000;
				}
				
				if(glow) AppEffect.glowFilterAni(varsText, color);
				
				jsonValidator.validate();
				updateHeight();
			}
			
			
			
			/**
			 * 更新该容器的高度
			 */
			private function updateHeight():void
			{
				openIcon.visible = !_isOpen;
				closeIcon.visible = _isOpen;
				varsText.visible = border.visible = _isOpen;
				
				_updateHeightCount = 0;
				Common.stage.addEventListener(Event.ENTER_FRAME, doUpdateHeight);
			}
			
			private var _updateHeightCount:int;
			
			private function doUpdateHeight(event:Event):void
			{
				_updateHeightCount++;
				if(_updateHeightCount == 5) Common.stage.removeEventListener(Event.ENTER_FRAME, doUpdateHeight);
				
				var height:int = int(varsText.textDisplay["contentHeight"]) + 5;
				varsText.height = height;
				border.height = varsText.y + height + 5;
				this.height = _isOpen ? border.height + 5 : 33;
			}
			
			
			
			protected function openOrClose(event:MouseEvent):void
			{
				_isOpen = !_isOpen;
				updateHeight();
			}
			
			
			
			protected function varsText_changeHandler(event:TextOperationEvent):void
			{
				updateHeight();
			}
			
			protected function varsText_focusInHandler(event:FocusEvent):void
			{
				AppCommon.keyboardUsing = true;
			}
			
			protected function varsText_focusOutHandler(event:FocusEvent):void
			{
				AppCommon.keyboardUsing = false;
				selectedLayer.vars = varsText.text;
				update();
			}
			
			protected function varsText_keyUpHandler(event:KeyboardEvent):void
			{
				if(event.ctrlKey && event.keyCode == Keyboard.S) {
					selectedLayer.vars = varsText.text;
					update(true);
				}
			}
			
			
			
			
			/**
			 * 当前选中的图层
			 */
			private function get selectedLayer():Layer { return _layer; }
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<validators:JsonValidator id="jsonValidator" required="false"
								  source="{varsText}" property="text"
								  trigger="{varsText}" triggerEvent="focusOut"
								  />
	</fx:Declarations>
	
	<s:BorderContainer id="border" x="5" width="235" height="65" borderColor="0xCCCCCC" >
	</s:BorderContainer>
	<s:TextArea id="varsText" x="9" y="35" width="227" height="25"
				fontFamily="宋体"
				change="varsText_changeHandler(event)"
				focusIn="varsText_focusInHandler(event)"
				focusOut="varsText_focusOutHandler(event)"
				keyUp="varsText_keyUpHandler(event)"
				/>
	
	<s:Label x="98" y="8" height="25" verticalAlign="middle" text="自定义变量"
			 click="openOrClose(event)" />
	<s:Image id="openIcon" x="83" y="15" click="openOrClose(event)" source="{ImageAssets.Add}" />
	<s:Image id="closeIcon" x="82" y="10" click="openOrClose(event)" source="{ImageAssets.Min}" />
</s:Group>
